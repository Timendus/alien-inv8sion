:include "display.8o"
:include "scores.8o"

: render-scene
  display-clear
  render-background
  render-enemies
  render-player
  render-hud
  render-power
  render-score
  render-level
  i := long level-screen
  load v0
  if v0 == 1 then render-level-text
  i := long game-over
  load v0
  if v0 == 4 then render-game-over-text
  display-copy
  return

: render-background
  i := long tick
  load vE - vE

  vD := vE
  vD >>= vD
  vD >>= vD
  vD >>= vD
  v0 := 7
  v0 &= vD
  v2 := 7
  v2 -= v0 # X
  v3 := 8 # Y
  loop
    :unpack long background2-sprite
    display-complex-sprite
    v2 += 8
    if v2 < 64 then
  again

  vD := vE
  vD >>= vD
  vD >>= vD
  v0 := 7
  v0 &= vD
  v2 := 7
  v2 -= v0 # X
  v3 := 12 # Y
  loop
    :unpack long background3-sprite
    display-complex-sprite
    v2 += 8
    if v2 < 64 then
  again

  vD := vE
  vD >>= vD
  v0 := 7
  v0 &= vD
  v2 := 7
  v2 -= v0 # X
  v3 := 17 # Y
  loop
    :unpack long background4-sprite
    display-complex-sprite
    v2 += 8
    if v2 < 64 then
  again

  v0 := 7
  v0 &= vE
  v2 := 7
  v2 -= v0 # X
  v3 := 22 # Y
  loop
    :unpack long background5-sprite
    display-complex-sprite
    v2 += 8
    if v2 < 64 then
  again

  vE >>= vE
  vE >>= vE
  vE >>= vE
  vE >>= vE
  v0 := 7
  v0 &= vE
  v2 := 7
  v2 -= v0 # X
  v3 := 0 # Y
  loop
    :unpack long background1-sprite
    display-complex-sprite
    v2 += 8
    if v2 < 64 then
  again

  return

: render-player
  :unpack long player-sprite
  i := long game-over
  load v2 - v2
  if v2 == 1 begin
    :unpack long explosion1-sprite
  end
  if v2 == 2 begin
    :unpack long explosion2-sprite
  end
  if v2 == 3 begin
    :unpack long explosion3-sprite
  end
  if v2 == 4 begin
    :unpack long explosion3-sprite
  end
  i := player
  load v2 - v3 # X and Y
  display-complex-sprite

  i := long game-over
  load v0
  if v0 != 0 then return

  # Show shield?
  i := hits
  load v0
  if v0 < 115 begin
    v3 -= 1
    if v0 > 100 begin
      :unpack long low-shield-sprite
    else
      :unpack long shield-sprite
    end
    display-complex-sprite
    v3 += 1
  end

  # Are we shooting?
  i := shooting
  load v0
  if v0 == 0 then return

  # Show lasers
  v2 += 3 # X
  i := tick
  load v0
  v1 := 3
  v0 &= v1
  v2 += v0
  v3 += 1 # Y
  loop
    :unpack long laser-sprite
    display-complex-sprite
    v3 += 4
    :unpack long laser-sprite
    display-complex-sprite
    v3 -= 4
    v2 += 6
    if v2 < 60 then
  again
  return

# This subroutine is also responsible for updating the enemy positions and for
# bouncing them at the edges of the screen.
: render-enemies
  # Pre-load current level for generate-enemy
  i := long level
  load vC - vC
  vE := 0
  loop
    if vE == ENEMY_BYTES then return # Checked all NUM_ENEMIES enemies

    i := long enemies
    i += vE
    load v0 - v4 # Don't auto-increment i

    if v0 != 0 begin # Is there an enemy in this slot?
      v2 += v1
      v3 += v4
      save v0 - v4 # Don't auto-increment i

      # Interpret X and Y coordinates as fixed point floats
      v2 >>= v2 v2 >>= v2
      v3 >>= v3 v3 >>= v3

      if v2 == 0 begin
        # Disappear on the left (and generate new enemy)
        generate-enemy
        i := long score
        load v0 - v0
        if v0 > 4 begin
          v0 -= 5
        else
          v0 := 0
        end
        save v0
      else
        # Bounce top and bottom
        if v3 == 0  then v4 := 1
        if v3 == 25 then v4 := -1
        v5 := 4
        i += v5
        save v4 - v4

        # Select the right sprite
        if v0 == 1 begin :unpack long enemy1-sprite end
        if v0 == 2 begin :unpack long enemy2-sprite end
        if v0 == 3 begin :unpack long explosion1-sprite end
        if v0 == 4 begin :unpack long explosion2-sprite end
        if v0 == 5 begin :unpack long explosion3-sprite end

        # And show that cannon fodder!
        display-complex-sprite
      end
    end
    vE += 5
  again

: render-hud
  # Plane 1
  i := long hud-top-sprite
  load vE
  i := long display-buffer-plane1
  save vE
  i := long hud-bottom-sprite
  load vE
  i := long display-buffer-plane1-plus-15
  save vE

  # Plane 2
  i := long hud-top-sprite
  load vE
  i := long display-buffer-plane2
  save vE
  i := long hud-bottom-sprite
  load vE
  i := long display-buffer-plane2-plus-15
  save vE

  # Plane 2
  i := long hud-top-sprite
  load vE
  i := long display-buffer-plane3
  save vE
  i := long hud-bottom-sprite
  load vE
  i := long display-buffer-plane3-plus-15
  save vE

  # Plane 2
  i := long hud-top-sprite
  load vE
  i := long display-buffer-plane4
  save vE
  i := long hud-bottom-sprite
  load vE
  i := long display-buffer-plane4-plus-15
  save vE

  return

: render-power
  i := long hits
  load vA - vA
  vA >>= vA
  vA >>= vA
  v2 := 0
  v3 := 0
  v4 := 1
  loop
    :unpack long power-bar
    if v3 >= vA begin
      v5 := 0
    else
      v5 := 2
    end
    display-sprite
    v3 += 1
    if v3 != 30 then
  again
  return

:include "sprites/backgrounds.8o"
:include "sprites/player.8o"
:include "sprites/enemies.8o"
:include "sprites/hud.8o"
