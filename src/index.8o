:include "macros.8o"

: main
  lores
  render-vanity
  v0 := 100
  delay := v0
  loop
    v0 := delay
    if v0 != 0 then
  again
: restart
  render-title
  v0 := key

: main-redraw
  # Sync to clock
  v0 := 1
  delay := v0
  loop
    v0 := delay
    if v0 != 0 then
  again

  clock-tick

  # Game over check
  i := long game-over
  load v0
  if v0 == 5 then jump reset
  if v0 == 0 begin
    # Skip these things if we're game over
    check-keys
    check-shots
    check-collisions
  end

  render-scene
  explode-enemies
  jump main-redraw

: clock-tick
  i := long tick
  load vA - vA
  vA += 1
  save vA - vA

  # Game over animation
  i := long game-over
  load v1 - v1
  if vA == 0 begin
    if v1 == 4 then v1 := 5
  end
  v2 := 0b00011111
  v2 &= vA
  if v2 == 0 begin
    if v1 == 3 then v1 := 4
  end
  v2 := 0b00001111
  v2 &= vA
  if v2 == 0 begin
    if v1 == 2 then v1 := 3
    if v1 == 1 then v1 := 2
  end
  save v1 - v1

  # Revive all enemies once in a while
  v2 := 0b00111111
  v2 &= vA
  if v2 == 0 then revive-enemies

  # Hide level overlay after a while
  v2 := 0b01111111
  v2 &= vA
  if v2 == 0 begin
    i := long level-screen
    save v2 - v2
  end

  # Regain power over time
  v2 := 0b00000001
  v2 &= vA
  if v2 == 0 begin
    i := long hits
    load v0 - v0
    if v0 != 0 then v0 -= 1
    save v0
  end
  return

: check-shots
  # Are we shooting?
  i := long shooting
  load v0
  if v0 == 0 then return

  # Shooting costs power
  i := long hits
  load v0 - v0
  if v0 == 120 begin
    i := long shooting
    v0 := 0
    save v0
    return # Can't shoot without power
  else
    v0 += 1
    save v0
  end

  # Check if we shot any aliens
  i := long player
  load vC - vD
  vE := 0
  loop
    i := long enemies
    i += vE
    load v0 - v3
    # Don't blow up dead aliens
    if v0 == 0 then jump check-shots-next-alien
    if v0 > 2 then jump check-shots-next-alien
    # Check if we hit this alien
    v2 >>= v2
    v2 >>= v2
    if v2 > vC begin # Enemy is in front of player
      v3 >>= v3
      v3 >>= v3
      v3 -= vD
      v3 += 2
      if v3 < 8 begin # Enemy is on same height as player
        # Hit! Set enemy to explosion
        v0 := 3
        save v0
        # Increast score
        i := long level
        load v1 - v1
        i := long score
        load v0 - v0
        v0 += 1
        if v1 < 4 then v0 += 1
        if v1 < 3 then v0 += 1
        if v1 < 2 then v0 += 1
        save v0 - v0
        # Next level if score reaches 250
        if v0 > 249 begin
          # Reset score to 0
          v0 := 0
          save v0
          # Reset tick so timing of overlay makes sense
          i := long tick
          save v0
          # Increase level
          i := long level
          load v0 - v0
          v0 += 1
          save v0
          # Show level overlay
          v0 := 1
          i := long level-screen
          save v0
          # Enemies back to start
          hide-enemies
        end
        # Make a beep
        v0 := 1
        buzzer := v0
      end
    end
    : check-shots-next-alien
    vE += 5
    if vE == ENEMY_BYTES then return # Checked all NUM_ENEMIES enemies?
  again

: check-collisions
  i := long player
  load vC - vD
  vE := 0
  loop
    i := long enemies
    i += vE
    load v0 - v3
    # Don't collide with dead or exploded aliens
    if v0 == 0 then jump check-collisions-next-alien
    if v0 > 2 then jump check-collisions-next-alien
    # Compare alien position (v2, v3) with player position (vC, vD)
    v2 >>= v2
    v2 >>= v2
    v2 -= vC
    v2 += 7
    if v2 < 15 begin
      v3 >>= v3
      v3 >>= v3
      v3 -= vD
      v3 += 3
      if v3 < 10 begin
        # Hit! Set enemy to explosion
        v0 := 3
        save v0
        # You get no points for this, but lose poser
        i := long hits
        load v0 - v0
        if v0 > 114 begin
          i := long game-over
          v0 := 1
          save v0
          i := long tick
          save v0
          v0 := 0
          i := long shooting
          save v0
        else
          v0 += 10
          save v0
        end
      end
    end
    : check-collisions-next-alien
    vE += 5
    if vE == ENEMY_BYTES then return # Checked all NUM_ENEMIES enemies?
  again

: reset
  i := long reset-values
  load v9
  i := long player
  save v9

  # Reset enemies
  i := long reset-enemies
  load v4
  vE := 0
  i := long enemies
  loop
    save v4
    vE += 1
    if vE != NUM_ENEMIES then # Checked all NUM_ENEMIES enemies?
  again

  jump restart

:segment data

: reset-values
  10 10 # Player X, Y
  0 # Shooting boolean
  1 # Level screen boolean
  0 # tick
  0 # wave
  1 # level
  0 # score
  0 # hits
  0 # Game-over state
: reset-enemies
  0 0 0 0 0

:include "enemies.8o"
:include "renderer1.8o"
:include "game-state.8o"
:include "key-input.8o"
:include "text.8o"
:include "title.8o"
